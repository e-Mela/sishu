sourceSets {
    contractTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDirs file('src/contractTest/java')
        }
        resources.srcDir file('src/contractTest/resources')
    }
}

configurations {
    contractTestImplementation.extendsFrom testImplementation
    contractTestRuntime.extendsFrom testRuntime
    contractTests
}

dependencies {
    contractTestImplementation('au.com.dius:pact-jvm-provider-junit_2.11:3.2.3')
}

task extractContractTests(type: Copy, dependsOn: configurations.contractTests) {
    from {
        configurations.contractTests.collect { zipTree(it) }
    }
    into "${sourceSets.contractTest.output.resourcesDir}/contract-tests"
}

task contractTest(type: Test, dependsOn: extractContractTests) {
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath

    reports.html.destination = file("${reporting.baseDir}/${name}")
}

idea {
    module {
        scopes.TEST.plus += [configurations.runtimeClasspath]
    }
}

